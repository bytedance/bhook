plugins {
    id 'com.android.library'
}

android {
    namespace 'com.bytedance.android.bytehook'
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    ndkVersion rootProject.ext.ndkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                abiFilters rootProject.ext.abiFilters.split(",")
                arguments "-DANDROID_STL=none"
                if(rootProject.ext.useASAN) {
                    arguments "-DANDROID_ARM_MODE=arm"
                    arguments "-DUSEASAN=ON"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version rootProject.ext.cmakeVersion
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    packagingOptions {
        exclude '**/libshadowhook.so'
        exclude '**/libshadowhook_nothing.so'
    }
    buildFeatures {
        prefabPublishing true
        buildConfig false
        prefab true
    }
    prefab {
        bytehook {
            headers "src/main/cpp/include"
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api 'com.bytedance.android:shadowhook:' + rootProject.ext.shadowhookVersion
}

apply from: rootProject.file('gradle/prefab_bypass.gradle')
apply from: rootProject.file('gradle/check.gradle')
apply from: rootProject.file('gradle/publish.gradle')
